plugins {
  id 'org.jetbrains.intellij' version '0.0.40'
}

version = "0001"
allprojects {
  apply plugin: 'java'
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
  tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

  sourceSets {
    main {
      java.srcDirs 'src', 'gen'
      resources.srcDirs 'resources', 'plugin/resources'
    }
    test {
      java.srcDir 'tests'
    }
  }

  apply plugin: 'org.jetbrains.intellij'
  intellij {
    version ideaVersion
    plugins 'copyright'
    downloadSources Boolean.valueOf(sources)
    sameSinceUntilBuild Boolean.valueOf(isEAP)
  }

  def compilationPackages = ['net/masterthought/dlanguage/build/**', 'net/masterthought/dlanguage/jps/**']

  test {
    useJUnit {
      exclude compilationPackages + 'net/masterthought/dlanguage/**/*TestBase*'
    }
    testLogging {
      exceptionFormat = 'full'
    }
  }

  task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
    useJUnit {
      include compilationPackages
    }
    testLogging {
      exceptionFormat = 'full'
    }
  }
}

repositories {
  flatDir {
    dirs 'libs'
  }
}

dependencies {
  compile project('jps-shared')
  compile project('jps-plugin')
}

apply plugin: 'idea'
idea {
  project {
    jdkName = javaVersion
    languageLevel = javaVersion
  }
  module {
    generatedSourceDirs += file('gen')
  }
}
